# Maintainer: Carlos Antunes <cmantunes@gmail.com>
# Contributed by: nanodude

_realname=newlib
_target=arm-none-eabi

pkgbase=mingw-w64-${_target}-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_target}-${_realname}")
pkgver=3.3.0
pkgrel=1
pkgdesc="A C standard library implementation intended for use on embedded systems (ARM bare metal)"
arch=('any')
url="https://sourceware.org/newlib/"
license=('Various')
groups=("${MINGW_PACKAGE_PREFIX}-${_target}-toolchain")
makedepends=("${MINGW_PACKAGE_PREFIX}-${_target}-gcc-base")
options=('staticlibs' '!strip' '!emptydirs')
source=(https://sourceware.org/pub/${_realname}/${_realname}-$pkgver.tar.gz)
sha256sums=('58dd9e3eaedf519360d92d84205c3deef0b3fc286685d1c562e245914ef72c66')
noextract=("${_realname}-${pkgver}.tar.gz")

prepare() {
  [[ -d "${_realname}-${pkgver}" ]] && rm -rf "${_realname}-${pkgver}"
  tar zxf "${srcdir}/${_realname}-${pkgver}.tar.gz" || true
  MSYS=winsymlinks:lnk tar zxf "${srcdir}/${_realname}-${pkgver}.tar.gz"
}

build() {
  if check_option "debug" "y"; then
    CFLAGS_FOR_TARGET="-O0"
  else
    CFLAGS_FOR_TARGET="-O2"
  fi
  CFLAGS_FOR_TARGET+=" -g -ffunction-sections -fdata-sections"
  export CFLAGS_FOR_TARGET

  cd $srcdir
  rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST} && cd build-${MINGW_CHOST}
  ../${_realname}-${pkgver}/configure \
  --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${_target} \
    --prefix=${MINGW_PREFIX} \
    --enable-newlib-io-long-long \
    --enable-newlib-io-c99-formats \
    --enable-newlib-register-fini \
    --enable-newlib-retargetable-locking \
    --disable-newlib-supplied-syscalls \
    --disable-nls \
    --with-{gmp,mpfr,mpc,isl,cloog}="${MINGW_PREFIX}" \
    --with-sysroot="${MINGW_PREFIX}" \
    --with-multilib-list=rmprofile

  make
}

package() {

  cd ${srcdir}/build-${MINGW_CHOST}
  make -j1 DESTDIR=${pkgdir} install

  # Remove files that conflict with host
  rm -rf "${pkgdir}${MINGW_PREFIX}/share/info"
}