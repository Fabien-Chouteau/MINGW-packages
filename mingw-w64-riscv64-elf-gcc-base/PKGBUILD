# Maintainer: Fabien Chouteau <chouteau@adacore.com>
# Arch Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Arch Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>

_realname=gcc-base
_target=riscv64-elf

pkgbase=mingw-w64-${_target}-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_target}-${_realname}")
pkgver=10.1.0
_islver=0.22
pkgrel=1
pkgdesc='The GNU Compiler Collection - cross compiler for RISCV64 target - bootstrap version. only used to build newlib'
arch=('any')
url='https://gcc.gnu.org/'
license=(GPL LGPL FDL)
depends=("${MINGW_PACKAGE_PREFIX}-${_target}-binutils"
		 "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=('staticlibs' '!debug' '!strip')

source=(https://ftp.gnu.org/gnu/gcc/gcc-$pkgver/gcc-$pkgver.tar.xz
        http://isl.gforge.inria.fr/isl-$_islver.tar.bz2)
sha256sums=('b6898a23844b656f1b68691c5c012036c2e694ac4b53a8918d4712ad876e7ea2'
            'b21d354acd613a91cb88328753ec3aaeb174d6af042d89c5fcf3bbcced370751')

prepare() {

  cd "$srcdir/gcc-$pkgver"

  # copy isl for in-tree builds
  cp -r ../isl-$_islver isl

  echo $pkgver > gcc/BASE-VER

  # hack! - some configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" {libiberty,gcc}/configure
}

build() {
  # Build GCC
  cd $srcdir
  rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST} && cd build-${MINGW_CHOST}

  unset CFLAGS
  unset CXXFLAGS
  export CFLAGS="-g -O3 -fdata-sections -ffunction-sections"
  export CXXFLAGS="${CFLAGS}"

  local _GCC_LDFLAGS="${LDFLAGS} -Wl,--disable-dynamicbase"
  if [ "${CARCH}" = 'x86_64' ]; then
    _GCC_LDFLAGS+=",--image-base=0x400000"
  fi
	
  ../gcc-${pkgver}/configure \
    --build="${MINGW_CHOST}" \
    --host="${MINGW_CHOST}" \
    --target="${_target}" \
    --prefix="${MINGW_PREFIX}" \
    --with-sysroot="${MINGW_PREFIX}" \
    --with-native-system-header-dir=/include \
    --libexecdir=${MINGW_PREFIX}/lib \
    --enable-languages="c" \
    --disable-libgomp \
    --disable-libmudflap \
    --disable-libquadmath \
    --disable-libssp \
    --disable-nls \
    --disable-shared \
    --disable-threads \
    --disable-tls \
    --with-gnu-as \
    --with-gnu-ld \
    --with-system-zlib \
    --with-python-dir="share/${_target}-${_realname}" \
    --with-{gmp,mpfr,mpc,isl,cloog}="${MINGW_PREFIX}" \
    --with-host-libstdcxx="-static-libgcc -Wl,-Bstatic,-lstdc++,-Bdynamic -lm" \
    --disable-multilib \
    LDFLAGS="${_GCC_LDFLAGS}"

  make all-gcc
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make -j1 DESTDIR=${pkgdir} install-gcc

  # Remove files that conflict with host
  rm -rf "${pkgdir}${MINGW_PREFIX}/share/info"
  rm -rf "${pkgdir}${MINGW_PREFIX}/share/man/man7"
}

